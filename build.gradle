plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.3'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id "com.epages.restdocs-api-spec" version "0.18.2"
}

group = 'com'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

ext {
    set("snippetsDir", file("build/generated-snippets"))
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

dependencies {

    //springboot
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    //data
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    //lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    //test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    //restdocs
    asciidoctorExt("org.springframework.restdocs:spring-restdocs-asciidoctor")
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation("com.epages:restdocs-api-spec-mockmvc:0.18.2")
}

tasks.named('test') {
    useJUnitPlatform()
}
jar.enabled = false

// asciidoctor start
asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    sources {
        include("**/index.adoc")
    }
    baseDirFollowsSourceFile()
    dependsOn test
}

asciidoctor.doFirst {
    delete file("src/main/resources/static/docs")
}
// asciidoctor end

openapi3 {
    servers = [
            { url = 'http://localhost:8080' },
    ]
    title = 'Project title'
    description = 'Project description'
    version = "${project.version}"
    format = 'yaml'
}

tasks.register('copyOasToSwagger', Copy) {
    dependsOn("openapi3"); // openapi3 Task가 먼저 실행되도록 설정
    delete("src/main/resources/static/swagger-ui/openapi3.yaml"); // 기존 OAS 파일 삭제
    from("build/api-spec/openapi3.yaml"); // 복제할 OAS 파일 지정
    into("src/main/resources/static/swagger-ui/."); // 타겟 디렉터리로 파일 복제
}

bootJar {
    dependsOn asciidoctor
    copy {
        from asciidoctor.outputDir
        into "src/main/resources/static/docs"
    }
    archiveFileName.set "app.jar"
}